/*
 * Nextcloud - Android Client
 *
 * SPDX-FileCopyrightText: 2016-2024 Nextcloud GmbH and Nextcloud contributors
 * SPDX-FileCopyrightText: 2024 Alper Ozturk <alper.ozturk@nextcloud.com>
 * SPDX-FileCopyrightText: 2024 Tobias Kaminsky <tobias@kaminsky.me>
 * SPDX-FileCopyrightText: 2024 Andy Scherzinger <info@andy-scherzinger.de>
 * SPDX-FileCopyrightText: 2022 √Ålvaro Brey Vilas <alvaro@alvarobrey.com>
 * SPDX-License-Identifier: AGPL-3.0-or-later OR GPL-2.0-only
 */
import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask
import org.gradle.internal.jvm.Jvm

buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.18'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.6"
        classpath "commons-httpclient:commons-httpclient:3.1@jar" // remove after entire switch to lib v2
        classpath 'com.karumi:shot:6.1.0'
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jacoco:org.jacoco.report:$jacoco_version"
        classpath "org.jacoco:org.jacoco.agent:$jacoco_version"
    }
}

plugins {
    id "org.jetbrains.kotlin.plugin.compose" version "2.0.0"
    id "com.diffplug.spotless" version "6.25.0"
    id "org.jetbrains.kotlin.kapt" version "2.0.0"
    id 'com.google.devtools.ksp' version '2.0.0-1.0.22' apply false
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply from: "$rootProject.projectDir/jacoco.gradle"
apply plugin: 'com.github.spotbugs'
apply plugin: 'io.gitlab.arturbosch.detekt'

// needed to make renovate run without shot, as shot requires Android SDK
// https://github.com/pedrovgs/Shot/issues/300
if (shotTest) {
    apply plugin: 'shot'
}
apply plugin: 'com.google.devtools.ksp'


println "Gradle uses Java ${Jvm.current()}"

configurations {
    configureEach {
        exclude group: 'org.jetbrains', module: 'annotations-java5' // via prism4j, already using annotations explicitly

        // check for updates every build
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
        }
    }
}


configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (requested.group == "org.checkerframework" && requested.name != "checker-compat-qual") {
            useVersion(checkerVersion)
            because("https://github.com/google/ExoPlayer/issues/10007")
        }
    }
}

// semantic versioning for version code
def versionMajor = 3
def versionMinor = 30
def versionPatch = 0
def versionBuild = 0 // 0-50=Alpha / 51-98=RC / 90-99=stable


def ndkEnv = new HashMap<String, String>()
file("$project.rootDir/ndk.env").readLines().each() {
    def (key, value) = it.tokenize('=')
    ndkEnv.put(key, value)
}

def perfAnalysis = project.hasProperty('perfAnalysis')

android {
    // install this NDK version and Cmake to produce smaller APKs. Build will still work if not installed
    ndkVersion "${ndkEnv.get("NDK_VERSION")}"


    namespace 'com.owncloud.android'
    testNamespace "${namespace}.test"

    androidResources {
        generateLocaleConfig = true
    }

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        compileSdk 34

        buildConfigField 'boolean', 'CI', ciBuild.toString()
        buildConfigField 'boolean', 'RUNTIME_PERF_ANALYSIS', perfAnalysis.toString()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // arguments to be passed to functional tests
        if (shotTest) {
            testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        } else {
            testInstrumentationRunner "com.nextcloud.client.TestRunner"
        }
        testInstrumentationRunnerArgument "TEST_SERVER_URL", "${NC_TEST_SERVER_BASEURL}"
        testInstrumentationRunnerArgument "TEST_SERVER_USERNAME", "${NC_TEST_SERVER_USERNAME}"
        testInstrumentationRunnerArgument "TEST_SERVER_PASSWORD", "${NC_TEST_SERVER_PASSWORD}"
        testInstrumentationRunnerArguments disableAnalytics: 'true'

        versionCode versionMajor * 10000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild

        if (versionBuild > 89) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        } else if (versionBuild > 50) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch} RC" + (versionBuild - 50)
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionPatch} Alpha" + (versionBuild + 1)
        }

        // adapt structure from Eclipse to Gradle/Android Studio expectations;
        // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure

        flavorDimensions += "default"

        buildTypes {
            debug {
                testCoverageEnabled(project.hasProperty('coverage'))
            }
        }

        buildFeatures {
            buildConfig = true
        }

        productFlavors {
            // used for f-droid
            generic {
                applicationId 'com.nextcloud.client'
                dimension "default"
            }

            gplay {
                applicationId 'com.nextcloud.client'
                dimension "default"
            }

            huawei {
                applicationId 'com.nextcloud.client'
                dimension "default"
            }

            versionDev {
                applicationId "com.nextcloud.android.beta"
                dimension "default"
                versionCode 20220322
                versionName "20220322"
            }

            qa {
                applicationId "com.nextcloud.android.qa"
                dimension "default"
                versionCode 1
                versionName "1"
            }
        }


        testOptions {
            unitTests.returnDefaultValues = true
            animationsDisabled true
        }
    }

    // adapt structure from Eclipse to Gradle/Android Studio expectations;
    // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure
    packagingOptions {
        resources {
            excludes += 'META-INF/LICENSE*'
            excludes += 'META-INF/versions/9/OSGI-INF/MANIFEST*'
            pickFirst 'MANIFEST.MF' // workaround for duplicated manifest on some dependencies
        }
    }

    tasks.register("checkstyle", Checkstyle) {
        configFile = file("${rootProject.projectDir}/checkstyle.xml")
        configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        classpath = files()
    }

    tasks.register("pmd", Pmd) {
        ruleSetFiles = files("${project.rootDir}/ruleset.xml")
        ignoreFailures = true // should continue checking
        ruleSets = []

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml {
                destination = file("$project.buildDir/reports/pmd/pmd.xml")
            }
            html {
                destination = file("$project.buildDir/reports/pmd/pmd.html")
            }
        }
    }

    check.dependsOn 'checkstyle', 'spotbugsGplayDebug', 'pmd', 'lint', 'spotlessKotlinCheck', 'detekt'

    buildFeatures {
        dataBinding true
        viewBinding true
        aidl true
        compose = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    lint {
        abortOnError false
        checkGeneratedSources true
        disable 'MissingTranslation', 'GradleDependency', 'VectorPath', 'IconMissingDensityFolder', 'IconDensities', 'GoogleAppIndexingWarning', 'MissingDefaultResource', 'InvalidPeriodicWorkRequestInterval', 'StringFormatInvalid', 'MissingQuantity'
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
        htmlReport true
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation("com.github.nextcloud:android-library:$androidLibraryVersion") {
        exclude group: 'org.ogce', module: 'xpp3' // unused in Android and brings wrong Junit version
    }

    // Jetpack Compose
    implementation(platform("androidx.compose:compose-bom:2024.06.00"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui-tooling-preview:1.6.8")
    debugImplementation 'androidx.compose.ui:ui-tooling:1.6.8'

    compileOnly 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'
    // remove after entire switch to lib v2
    implementation "commons-httpclient:commons-httpclient:3.1@jar" // remove after entire switch to lib v2
    implementation 'org.apache.jackrabbit:jackrabbit-webdav:2.13.5' // remove after entire switch to lib v2
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation 'androidx.webkit:webkit:1.11.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2"
    implementation "androidx.lifecycle:lifecycle-service:2.8.2"
    implementation "androidx.work:work-runtime:$workRuntime"
    implementation "androidx.work:work-runtime-ktx:$workRuntime"
    implementation "androidx.fragment:fragment-ktx:1.8.1"
    implementation 'com.github.albfernandez:juniversalchardet:2.0.3' // need this version for Android <7
    compileOnly 'com.google.code.findbugs:annotations:3.0.1u2'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.12.1'
    implementation 'org.lukhnos:nnio:0.3.1'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.nextcloud-deps:sectioned-recyclerview:0.6.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.28'
    implementation 'com.github.nextcloud-deps:qrcodescanner:0.1.2.4' // 'com.github.blikoon:QRCodeScanner:0.1.2'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation('com.github.bumptech.glide:glide:3.8.0') {
        exclude group: "com.android.support"
    }
    implementation 'com.caverock:androidsvg:1.4'
    implementation 'androidx.annotation:annotation:1.8.0'
    implementation 'com.vanniktech:emoji-google:0.18.0'

    implementation "com.github.nextcloud-deps.hwsecurity:hwsecurity-fido:$fidoVersion"
    implementation "com.github.nextcloud-deps.hwsecurity:hwsecurity-fido2:$fidoVersion"

    // document scanner not available on FDroid (generic) due to OpenCV binaries
    gplayImplementation project(':appscan')
    huaweiImplementation project(':appscan')
    qaImplementation project(':appscan')

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
    spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.6.4'

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    implementation "androidx.media3:media3-ui:$androidxMediaVersion"
    implementation "androidx.media3:media3-exoplayer:$androidxMediaVersion"
    implementation "androidx.media3:media3-datasource-okhttp:$androidxMediaVersion"

    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'

    // Shimmer animation
    implementation 'io.github.elye:loaderviewlibrary:3.0.0'

    // dependencies for markdown rendering
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"
    implementation "io.noties.markwon:ext-tasklist:$markwonVersion"
    implementation "io.noties.markwon:html:$markwonVersion"

    implementation "io.noties.markwon:syntax-highlight:$markwonVersion"
    implementation "io.noties:prism4j:$prismVersion"
    kapt "io.noties:prism4j-bundler:$prismVersion"

    // dependencies for image cropping and rotation
    implementation 'com.vanniktech:android-image-cropper:4.5.0'

    implementation 'org.osmdroid:osmdroid-android:6.1.18'

    implementation('org.mnode.ical4j:ical4j:3.0.0') {
        ['org.apache.commons', 'commons-logging'].each {
            exclude group: "$it"
        }
    }

    if (perfAnalysis) {
        debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
    }

    // dependencies for local unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "androidx.test:core:$androidxTestVersion"
    testImplementation 'org.json:json:20240303'
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "io.mockk:mockk-android:$mockkVersion"

    // dependencies for instrumented tests
    // JUnit4 Rules
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "androidx.test:rules:$androidxTestVersion"
    // Android JUnit Runner
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestUtil "androidx.test:orchestrator:1.4.2"
    androidTestImplementation "androidx.test:core-ktx:$androidxTestVersion"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    // Mocking support
    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5' // required to allow mocking on API 27 and older
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation("org.mockito:mockito-android:$mockitoVersion")
    androidTestImplementation "io.mockk:mockk-android:$mockkVersion"
    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'
    androidTestImplementation "com.facebook.testing.screenshot:core:0.15.0"

    // UIAutomator - for cross-app UI tests, and to grant screen is turned on in Espresso tests
    // androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    // fix conflict in dependencies; see http://g.co/androidstudio/app-test-app-conflict for details
    //androidTestImplementation "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.github.stateless4j:stateless4j:2.6.0"

    // upon each update first test: new registration, receive push
    gplayImplementation "com.google.firebase:firebase-messaging:24.0.0"
    gplayImplementation 'com.google.android.gms:play-services-base:18.5.0'
    gplayImplementation 'com.google.android.play:review-ktx:2.0.1'

    implementation 'com.github.nextcloud.android-common:ui:0.17.0'

    implementation "androidx.room:room-runtime:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    implementation "io.coil-kt:coil:2.6.0"

    // splash screen dependency ref: https://developer.android.com/develop/ui/views/launch/splash-screen/migrate
    implementation 'androidx.core:core-splashscreen:1.0.1'
}

configurations.configureEach {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
        force 'org.objenesis:objenesis:3.4'
        eachDependency { details ->
            if ('org.jacoco' == details.requested.group) {
                details.useVersion "$jacoco_version"
            }
        }
    }
}

tasks.withType(Test).configureEach {
    // increased logging for tests
    testLogging {
        events "passed", "skipped", "failed"
    }
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output -> outputFileName = "${output.baseName}-${variant.versionCode}.apk"
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint()
    }
}

detekt {
    config.setFrom("detekt.yml")
}

if (shotTest) {
    shot {
        showOnlyFailingTestsInReports = ciBuild
        // CI environment renders some shadows slightly different from local VMs
        // Add a 0.5% tolerance to account for that
        tolerance = ciBuild ? 0.5 : 0
    }
}

jacoco {
    toolVersion = "$jacoco_version"
}

spotbugs {
    ignoreFailures = true // should continue checking
    effort = Effort.MAX
    reportLevel = Confidence.valueOf('MEDIUM')
}

tasks.withType(SpotBugsTask){task ->
    String variantNameCap = task.name.replace("spotbugs", "")
    String variantName = variantNameCap.substring(0, 1).toLowerCase() + variantNameCap.substring(1)
    dependsOn "compile${variantNameCap}Sources"

    classes = fileTree("$project.buildDir/intermediates/javac/${variantName}/compile${variantNameCap}JavaWithJavac/classes/")
    excludeFilter = file("${project.rootDir}/scripts/analysis/spotbugs-filter.xml")
    reports {
        xml {
            required = true
        }
        html {
            required = true
            outputLocation = file("$project.buildDir/reports/spotbugs/spotbugs.html")
            stylesheet = 'fancy.xsl'
        }
    }
}

ksp {
    arg('room.schemaLocation', "$projectDir/schemas")
}
