// Gradle build file
//
// This project was started in Eclipse and later moved to Android Studio. In the transition, both IDEs were supported.
// Due to this, the files layout is not the usual in new projects created with Android Studio / gradle. This file
// merges declarations usually split in two separates build.gradle file, one for global settings of the project in
// its root folder, another one for the app module in subfolder of root.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'org.codehaus.groovy:groovy-all:2.4.12' 
        classpath('com.dicedmelon.gradle:jacoco-android:0.1.2') { 
            exclude group: 'org.codehaus.groovy', module: 'groovy-all' 
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jacoco-android'

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
    supportLibraryVersion = '27.1.1'
    googleLibraryVersion = '12.0.1'
    androidLibraryVersion = '1.0.40'
    jacocoVersion = "0.7.4.201502262128"

    travisBuild = System.getenv("TRAVIS") == "true"

    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    google()

    flatDir {
        dirs 'libs'
    }
}

// semantic versioning for version code
def versionMajor = 3
def versionMinor = 2
def versionPatch = 0
def versionBuild = 99 // 0-49=Alpha / 50-98=RC / 99=stable

android {
    lintOptions {
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
        disable 'MissingTranslation'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // arguments to be passed to functional tests
        testInstrumentationRunnerArgument "TEST_USER", "\"$System.env.OCTEST_APP_USERNAME\""
        testInstrumentationRunnerArgument "TEST_PASSWORD", "\"$System.env.OCTEST_APP_PASSWORD\""
        testInstrumentationRunnerArgument "TEST_SERVER_URL", "\"$System.env.OCTEST_SERVER_BASE_URL\""

        multiDexEnabled true

        versionCode versionMajor * 10000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild

        if (versionBuild > 98) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        } else if (versionBuild > 49) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch} RC"+(versionBuild-49)
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionPatch} Alpha"+(versionBuild+1)
        }

        // adapt structure from Eclipse to Gradle/Android Studio expectations;
        // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure

        flavorDimensions "default"

        buildTypes {
            debug {
                testCoverageEnabled true
            }
        }

        productFlavors {
            // used for f-droid
            generic {
                applicationId 'com.nextcloud.client'
                dimension "default"
            }

            gplay {
                applicationId 'com.nextcloud.client'
                dimension "default"
            }

            versionDev {
                applicationId "com.nextcloud.android.beta"
                dimension "default"
                versionCode 20180526
                versionName "20180526"
            }
        }
    }

    // adapt structure from Eclipse to Gradle/Android Studio expectations;
    // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
    }

    task checkstyle(type: Checkstyle) {
        configFile = file("${rootProject.projectDir}/checkstyle.xml")
        configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        classpath = files()
    }

    task pmd(type: Pmd) {
        ruleSetFiles = files("${project.rootDir}/pmd-ruleset.xml")
        ignoreFailures = false
        ruleSets = []

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            xml {
                destination = file("$project.buildDir/reports/pmd/pmd.xml")
            }
            html {
                destination = file("$project.buildDir/reports/pmd/pmd.html")
            }
        }
    }

    task findbugs(type: FindBugs) {
        ignoreFailures = false
        effort = "max"
        reportLevel = "high"
        classes = files("$project.buildDir/intermediates/classes")
        excludeFilter = new File("${project.rootDir}/findbugs-filter.xml")
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            html {
                destination  = file("$project.buildDir/reports/findbugs/findbugs.html")
            }
        }
        classpath = files()
    }
    check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // dependencies for app building
    implementation 'com.android.support:multidex:1.0.3'
//    implementation project('nextcloud-android-library')
    genericImplementation "com.github.nextcloud:android-library:1.0.43"
    gplayImplementation "com.github.nextcloud:android-library:1.0.43"
    versionDevImplementation 'com.github.nextcloud:android-library:1.0.43' // use always latest master
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support:exifinterface:${supportLibraryVersion}"
    implementation 'com.github.albfernandez:juniversalchardet:v2.0.0'
    implementation 'com.google.code.findbugs:annotations:2.0.1'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.github.evernote:android-job:v1.2.5'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.10.2'
    implementation 'org.lukhnos:nnio:0.2'
    implementation 'com.madgag.spongycastle:pkix:1.54.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.afollestad:sectioned-recyclerview:0.5.0'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'

    // uncomment for gplay
    // implementation "com.google.firebase:firebase-messaging:${googleLibraryVersion}"
    // implementation "com.google.android.gms:play-services-gcm:${googleLibraryVersion}"
    implementation 'org.parceler:parceler-api:1.1.9'
    annotationProcessor 'org.parceler:parceler:1.1.9'
    implementation ('com.github.bumptech.glide:glide:3.7.0') {
        exclude group: "com.android.support"
    }
    implementation 'com.caverock:androidsvg:1.2.1'
    implementation "com.android.support:support-annotations:${supportLibraryVersion}"
    implementation 'com.google.code.gson:gson:2.8.2'

    // dependencies for local unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    // dependencies for instrumented tests
    // JUnit4 Rules
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // Android JUnit Runner
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    // Espresso core
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    // UIAutomator - for cross-app UI tests, and to grant screen is turned on in Espresso tests
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    // fix conflict in dependencies; see http://g.co/androidstudio/app-test-app-conflict for details
    //androidTestImplementation "com.android.support:support-annotations:${supportLibraryVersion}"
    implementation 'org.jetbrains:annotations:15.0'

    androidTestImplementation 'tools.fastlane:screengrab:1.0.0'
    
//    jacocoAnt "org.jacoco:org.jacoco.ant:${jacocoVersion}"
//    jacocoAgent "org.jacoco:org.jacoco.agent:${jacocoVersion}"
//    androidJacocoAgent "org.jacoco:org.jacoco.agent:${jacocoVersion}"
//    androidJacocoAnt "org.jacoco:org.jacoco.ant:${jacocoVersion}"
//    androidJacocoAnt "org.jacoco:org.jacoco.core:${jacocoVersion}"
//    androidJacocoAnt "org.jacoco:org.jacoco.report:${jacocoVersion}"
//    androidJacocoAnt "org.jacoco:org.jacoco.agent:${jacocoVersion}"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.withType(Test) {
    // increased logging for tests
    testLogging {
        events "passed", "skipped", "failed"
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = "${output.baseName}-${variant.versionCode}.apk"
    }
}

task combinedTestReport(type: JacocoReport) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/gplay/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testGplayDebugUnitTest.exec', 'outputs/code-coverage/connected/flavors/GPLAY/*coverage.ec'
    ])
}

    // uncomment for gplay (must be at the bottom)
    // apply plugin: 'com.google.gms.google-services'
