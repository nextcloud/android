// Gradle build file
//
// This project was started in Eclipse and later moved to Android Studio. In the transition, both IDEs were supported.
// Due to this, the files layout is not the usual in new projects created with Android Studio / gradle. This file
// merges declarations usually split in two separates build.gradle file, one for global settings of the project in
// its root folder, another one for the app module in subfolder of root.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
    supportLibraryVersion = '25.0.0'

    travisBuild = System.getenv("TRAVIS") == "true"

    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    flatDir {
        dirs 'libs'
    }
}


android {
    lintOptions {
        abortOnError true
        lintConfig file("${project.rootDir}/lint.xml")
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // arguments to be passed to functional tests
        testInstrumentationRunnerArgument "TEST_USER", "\"$System.env.OCTEST_APP_USERNAME\""
        testInstrumentationRunnerArgument "TEST_PASSWORD", "\"$System.env.OCTEST_APP_PASSWORD\""
        testInstrumentationRunnerArgument "TEST_SERVER_URL", "\"$System.env.OCTEST_SERVER_BASE_URL\""

        multiDexEnabled true

        // adapt structure from Eclipse to Gradle/Android Studio expectations;
        // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure

        productFlavors {
            // used for f-droid
            generic {
                applicationId 'com.nextcloud.client'
            }

            gplay {
                applicationId 'com.nextcloud.client'
            }

            modified {
                // structure is:
                // domain tld
                // domain name
                // .client
                applicationId 'com.custom.client'
            }
        }

        configurations {
            modifiedCompile
        }
    }


    // adapt structure from Eclipse to Gradle/Android Studio expectations;
    // see http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Configuring-the-Structure

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
    }

    task checkstyle(type: Checkstyle) {
        configFile = file("${rootProject.projectDir}/checkstyle.xml")
        configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        classpath = files()
    }

    task pmd(type: Pmd) {
        ruleSetFiles = files("${project.rootDir}/pmd-ruleset.xml")
        ignoreFailures = false
        ruleSets = []

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            xml {
                destination "$project.buildDir/reports/pmd/pmd.xml"
            }
            html {
                destination "$project.buildDir/reports/pmd/pmd.html"
            }
        }
    }

    task findbugs(type: FindBugs) {
        ignoreFailures = false
        effort = "max"
        reportLevel = "high"
        classes = files("$project.buildDir/intermediates/classes")
        excludeFilter = new File("${project.rootDir}/findbugs-filter.xml")
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            html {
                destination "$project.buildDir/reports/findbugs/findbugs.html"
            }
        }
        classpath = files()
    }
    check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

}

dependencies {
    /// dependencies for app building
    compile name: 'touch-image-view'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.github.nextcloud:android-library:recognizeMaintenanceMode-SNAPSHOT'
    compile "com.android.support:support-v4:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.google.code.findbugs:annotations:2.0.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile 'com.github.evernote:android-job:v1.1.9'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.googlecode.ez-vcard:ez-vcard:0.10.2'

    // uncomment for gplay, modified
    //compile 'com.google.android.gms:play-services:10.2.4'

    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.caverock:androidsvg:1.2.1'

    /// dependencies for local unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    /// dependencies for instrumented tests
    // JUnit4 Rules
    androidTestCompile 'com.android.support.test:rules:0.5'

    // Android JUnit Runner
    androidTestCompile 'com.android.support.test:runner:0.5'

    // Android Annotation Support
    androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"

    // Espresso core
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    // UIAutomator - for cross-app UI tests, and to grant screen is turned on in Espresso tests
    //androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    // fix conflict in dependencies; see http://g.co/androidstudio/app-test-app-conflict for details
    //androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"

}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.withType(Test) {
    /// increased logging for tests
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// uncomment for gplay, modified (must be at the bottom)
//apply plugin: 'com.google.gms.google-services'